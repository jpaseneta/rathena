// Settings :
//	-	Only required to edit the ShopSetting() Function
//			Notes : You may also add / remove Menu ( If any ) 
//	-	Shop Currency can be either ItemID or Variable Name, but must write within Quotation Marks ( "" )
//			Ex. of Variable.	->	Zeny , #CASHPOINTS , #KAFRAPOINTS , CustomVariable , #CustomVariable
//	-	ERROR Message are used to show Invalid Settings in your NPC.


// Leave this alone...
-	shop	Emistry_Shop	-1,512:100


prontera,149,188,5	script	Impulse Shop	757,{
	function ShopSettings;
	function ValidateCost;
	function CurrencyInfo;
	function ClearData;
	function ValueConvert;
	function ErrorNotice;

	mes "Welcome to ^0000FFImpulse Shop^000000.";
	mes "Each shop may purchase using ^FF0000Different Currency^000000.";
	mes "^00FF00____________________________^000000";
	mes "So,Which shop you would like to look at it";
	next;
	// Menu Selection
	select("Proof of Donation","Impulse Trading Card","Cash Points");

	ClearData();
	ShopSettings( @menu );
	npcshopitem "Emistry_Shop",512,100;
	npcshopdelitem "Emistry_Shop",512;
	for(set .@i,0; .@i < getarraysize( @ItemLists ); set .@i,.@i+1)
		npcshopadditem "Emistry_Shop",@ItemLists[.@i],@ItemCost[.@i];
	mes "Okay...wait awhile";
	mes "^00FF00____________________________^000000";
	CurrencyInfo( @Currency$ );
	mes "^00FF00____________________________^000000";
	callshop "Emistry_Shop",1;
	npcshopattach "Emistry_Shop";
	end;


	function	ShopSettings	{
		switch( getarg(0) ){
			Case 1:
				// POD SHOP
				// Currency [ Item ID / Variable Name ]
				set @Currency$,"7179";
				dispbottom strnpcinfo(1)+": Accepted Payment Method: "+ getitemname(atoi(@Currency$));
				dispbottom strnpcinfo(1)+": You have " + countitem(atoi(@Currency$))+" "+ getitemname(atoi(@Currency$));
				dispbottom strnpcinfo(1)+": Notice: Discount Skill won't work at me, you'll have to pay the full price.";
				// Item ID Lists
				setarray @ItemLists[0],5374,5518,5137,5800,5288,5151,5210,5376,5305,5211,5325,5224,5293,5462,5389,5313,5539,5314,5377,5357,5361,5362,5463,5393,5481,5564,5423,5397,5508,18539,5592,5453,5452,5451,5428,5788,5155,5490,18670,5574;
				// Item Price
				setarray @ItemCost[0],12,12,8,8,5,5,6,6,5,5,7,8,9,7,4,7,16,8,6,3,4,7,8,7,6,10,10,4,4,12,7,9,11,16,10,5,8,5,12,8;
				break;
			Case 2:
				// TCG SHOP
				// Currency [ Item ID / Variable Name ]
				set @Currency$,"7227";
				dispbottom strnpcinfo(1)+": Accepted Payment Method: "+ getitemname(atoi(@Currency$));
				dispbottom strnpcinfo(1)+": You have " + countitem(atoi(@Currency$))+" "+ getitemname(atoi(@Currency$));
				dispbottom strnpcinfo(1)+": Notice: Discount Skill won't work at me, you'll have to pay the full price.";
				// Item ID Lists
				setarray @ItemLists[0],2306,2302,2303,2304,2305,2301;
				// Item Price
				setarray @ItemCost[0],2,2,3,4,5,6;
				break;
			Case 3:
				// Currency [ Item ID / Variable Name ]
				set @Currency$,"#CASHPOINTS";
				dispbottom strnpcinfo(1)+": Accepted Payment Method: Cashpoints";
				dispbottom strnpcinfo(1)+": You have " + getd( @Currency$ )+" Cashpoints";
				dispbottom strnpcinfo(1)+": Notice: Discount Skill won't work at me, you'll have to pay the full price.";
				// Item ID Lists
				setarray @ItemLists[0],2306,2302,2303,2304,2305,2301;
				// Item Price
				setarray @ItemCost[0],20,22,34,445,52,641;
				break;
			// Case 4,5,6.....etc...
			default:
				ErrorNotice( "Invalid Menu Selection for Menu "+@menu+"." );
				close;
		}
		
		
	if( @Currency$ == "" )
		ErrorNotice( "Invalid Currency Setting in Menu "+@menu+" ." );
	if( getarraysize( @ItemCost ) != getarraysize( @ItemLists ) || getarraysize( @ItemLists ) != getarraysize( @ItemCost ) )
		ErrorNotice( "Missing or Extra Value of Item or Cost Settings in Menu "+@menu+" ." );
	return;
	}

	function	ErrorNotice	{
		mes "^FF0000ERROR^000000 - "+getarg(0);
		mes "^00FF00____________________________^000000";
		mes "Inform this Message to ^0000FFGame Staffs^000000 immediately !";
		close;
	}

	function	CurrencyInfo	{
		if( getitemname( atoi( getarg(0) ) ) != "null" ){
			mes "Item Currency : ^FF0000"+getitemname( atoi( getarg(0) ) )+"^000000";
			mes "Available Amount : ^0000FF"+ValueConvert( countitem( atoi( getarg(0) ) ) )+"^000000";
		}else if( getitemname( atoi( getarg(0) ) ) == "null" ){
			mes "Variable Currency : ^FF0000"+getarg(0)+"^000000";
			mes "Available Amount : ^0000FF"+ValueConvert( getd( getarg(0) ) )+"^000000";
		}
	return;
	}

	function	ValidateCost	{
		if( getitemname( atoi( getarg(0) ) ) != "null" ){
			if( countitem( atoi( getarg(0) ) ) < getarg(1) ) return 1;
		}else{
			if( getd( getarg(0) ) < getarg(1) ) return 1;
		}
	return 0;
	}

	function ClearData	{
		set @Currency$,"";
		set @TotalCost,0;
		deletearray @bought_nameid[0],getarraysize( @bought_nameid );
		deletearray @bought_quantity[0],getarraysize( @bought_quantity );
		deletearray @ItemLists[0],getarraysize( @ItemLists );
		deletearray @ItemCost[0],getarraysize( @ItemCost );
	return;
	}

	function	ValueConvert	{
		set .@num, atoi(""+getarg(0));
		if ( .@num == 0 || .@num >= 2147483647 ) return getarg(0);
		set .@l, getstrlen(""+.@num);
		for ( set .@i,0; .@i < .@l; set .@i, .@i + 1 ) {
			set .@num$, .@num % pow(10,.@i+1) / pow(10,.@i) + .@num$;
				if ( (.@i+1) % 3 == 0 && .@i+1 != .@l ) set .@num$, ","+ .@num$;
		}
		return .@num$;
	}

	OnBuyItem:
		// ShopSettings( @menu );
		for(set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1)
			for(set @j,0; @j < getarraysize( @ItemLists ); set @j,@j+1)
				if( @ItemLists[@j] == @bought_nameid[@i] )
				set @TotalCost,@TotalCost + ( @ItemCost[@j] * @bought_quantity[@i] );
		mes "^FF0000       BILLING LIST^000000";
		mes "^00FF00____________________________^000000";
		for( set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1 )
				mes "^FF0000"+@bought_quantity[@i]+" x ^0000FF"+getitemname( @bought_nameid[@i] )+"^000000";
		mes "^00FF00____________________________^000000";

		if( getitemname( atoi( @Currency$ ) ) != "null" )
			mes "Total Cost : ^0000FF"+ValueConvert( @TotalCost )+" x "+getitemname( atoi( @Currency$ ) )+"^000000";
		else if( getitemname( atoi( @Currency$ ) ) == "null" ){
			mes "Total Cost : ^0000FF"+ValueConvert( @TotalCost )+" "+@Currency$+"^000000";
		}
		
		mes "^00FF00____________________________^000000";
		if( ValidateCost( @Currency$,@TotalCost ) ){
			if( getitemname( atoi( @Currency$ ) ) != "null" )
				mes "[ ^FF0000X^000000 ] Insufficient ^0000FF"+getitemname( atoi( @Currency$ ) )+"^000000";
			else{
				mes "[ ^FF0000X^000000 ] Insufficient ^0000FF"+@Currency$+"^000000";
			}
		}else{
			if( select( "^0000FFPurchase^000000:Cancel" ) == 1 ){
				if( getitemname( atoi( @Currency$ ) ) != "null" )
					delitem atoi( @Currency$ ),@TotalCost;
				else{
					set getd( @Currency$ ),getd( @Currency$ ) - @TotalCost;
				}
				for(set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1)
					getitem @bought_nameid[@i],@bought_quantity[@i];
				message strcharinfo(0),"Purchased "+getarraysize( @bought_nameid )+" Items.";
				
				if( getitemname( atoi( @Currency$ ) ) != "null" )
					dispbottom strnpcinfo(1)+": You have "+ countitem(atoi(@Currency$)) +" "+getitemname(atoi(@Currency$)) + " left.";
				else if( getitemname( atoi( @Currency$ ) ) == "null" ){
					dispbottom strnpcinfo(1)+": You have "+getd( @Currency$ )+" "+"cashpoints left.";
				}
				
				mes "Thank you for shopping.";
			}
		}
	ClearData();
	close;
	
	OnInit:
	waitingroom "[Impulse Shop]", 0;
	end;

}